buildscript {
    ext {
        gradlePluginVersion = '3.3.1'
        kotlinVersion = '1.3.11'
        kotlinCoroutinesVersion = '1.1.0'
        supportLibVersion = '27.1.1'
        tagSoupVersion = '1.2.1'
        glideVersion = '4.10.0'
        picassoVersion = '2.5.2'
        robolectricVersion = '4.3.1'
        jUnitVersion = '4.12'
        jSoupVersion = '1.11.3'
        wordpressUtilsVersion = '1.21'
        espressoVersion = '3.0.1'
        fetchstyleVersion = '1.1'
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradlePluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.automattic.android:fetchstyle:$fetchstyleVersion"
    }
}

apply plugin: 'com.automattic.android.fetchstyle'

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    configurations {
        ktlint
    }

    dependencies {
        ktlint 'com.github.shyiko:ktlint:0.9.2'
    }

    task ktlint(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "src/**/*.kt"
    }

    task ktlintFormat(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "-F", "src/**/*.kt"
    }
}
